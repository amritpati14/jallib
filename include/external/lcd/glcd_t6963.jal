-- ------------------------------------------------------
-- Title: Character/graphic LCD with Toshiba T6963C controller
--
-- Author: Rob Hamerling,  Copyright (c) 2015-2015, all rights reserved
--
-- Adapted-by: 
--
-- Compiler: 2.4q3
--
-- Revision: $Revision:$
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Library to handle character/graphics LCD with Toshiba T6963c controller
-- Tested with thi following modules: 
--        TW-7894V-0 (120x64)
--        MG2406G    (240x64)   
--        MTG-2460G  (240x64)
-- .
-- The t6963 ia a rather special glcd controller: it supports an LCD as
-- text and as graphics LCD simultaneously and independently. 
-- .
-- Connector pins of these modules: 
--   pin   symb   I/O   description
--    1     FG          frame ground
--    2    GND          ground     
--    3    VCC          lcd logic voltage 
--    4    VEE          lcd (contrast) voltage
--    5     wr     O    write enable (L)
--    6     rd     O    read enable (L)
--    7     ce     O    chip enable (L)
--    8     cd     O    H: command, L: data 
--    9                 not connected  
--   10   reset    O    reset (L)
--  11-18 db0-7   I/O   data byte
--   19     fs     O    font select (H: 6X8, L: 8x8)
--   20                 not connected
-- Other modules may have a different pin layout!
-- Of the TW-7894V-0 pin 19 seems not connected: fixed 6x8 font.
-- Of the MG2406G pin 9 is Vee: negative voltage for LCD contrast.
-- .
-- The following aliases must be defined by the user program:
-- .
-- Data port and pins:
--    alias t6963_dataport  is portX
--    alias t6963_dataport_direction is portX_direction
-- The library requires that all 8 data pins are wired to the
-- same PIC-port and in sequence 'd0' to pin_X0, 'd1' to pin_X1, etc.
-- All pins must be capable of digital output and input.
-- The port direction is dynamically selected by the library.
-- .
-- Control pins:
--    alias t6963_wr        is pin_Ya      -- write enable
--    alias t6963_rd        is pin_Yb      -- read enable
--    alias t6963_cd        is pin_Yc      -- command or data selection
--    alias t6963_ce        is pin_Yd      -- chip enable
--    alias t6963_reset     is pin_Ye      -- reset controller
--    alias t6963_fs        is pin_Yf      -- font select 
-- The control pins may be connected to any pin of the PIC capable
-- of digital output and must be set for output by the user program.
-- The font selection pin of the glcd module may be hard wired or the module
-- may have a fixed font width. In that case the specification of t6963_fs
-- is not required.
-- .
-- The following constant declarations are required:
--   const byte T6963X_PIXELS    = 120    )
--   const byte T6963Y_PIXELS    = 64     ) example values
--   const byte T6963_FONT_WIDTH = 6      ) 
-- T6963FONT_WIDTH is optional, when not specified an 8 pixels wide font is assumed.
-- Note: - The line length is derived from X-pixels and font width.
--         The T6963c performs auto-wrapping of text and attributes based
--         on this line length.
--       - When the specified font width does not match with the hardware
--         you may not see what you expected to see.
-- For graphics display the following constants and variables may be needed:
--   const bit T6963_CLIPPING   = TRUE   (default is FALSE)
--   var   bit t6963_pen_color  = ON     (default, OFF = clear pixel)
-- .
-- T6963c memory organisation as used by this library.
--    +-----------------------+ 0x0000
--    |                       |
--    |      Text Space       |  
--    |    (1/4 of total)     |
--    + ----------------------+ 0x....
--    |                       |
--    |   Graphics Space      |
--    |                       |
--    |                       |
--    +-----------------------+ 0x....
--    |   CG area (2KB)       |
--    +-----------------------+
-- Text and Graphics space depend on available memory, 
-- determined by t6963_init() by probing (max 64KB).
-- This memory layout is hardly of interest for user programs.
-- .
-- This library supports a subset of the features of the T6963c controller.
-- Currently not supported are for example:
--   - screen peek and copy (combined text and graphics memory)
--   - modules with 'dual scan'
-- .
-- Public procedures and functions:
--    t6963_init()
--    t6963_set_mode()
--    t6963_display_mode()
--    t6963_clear_text()
--    t6963_clear_graphics()
--    t6963_clear_screen() 
--    t6963_read_char()
--    t6963_read_next_char()
--    t6963_write_char()
--    t6963_write_next_char()
--    t6963_write_string()
--    t6963_set_cursor_pattern()
--    t6963_set_cursor_position()
--    t6963_set_attribute()
--    t6963_write_pixel()
--    t6963_read_cg_memory()
--    t6963_write_cg_memory()
-- .
-- The library 't6963_common' may be included by the user program for
-- drawing lines, rectangulars, blocks, ellipses, etc. and the library
-- math.jal for trigonometric functions.
--
-- Sources: http://www.lcd-module.de/fileadmin/eng/pdf/zubehoer/t6963c.pdf        
--          https://www.crystalfontz.com/controllers/Toshiba/T6963C/360
--
-- Notes:
--
-- --------------------------------------------------------------------------

-- some basic sanity checks
--
if ( !defined(T6963_FONT_WIDTH) ) then
   _warn "T6963 - Missing font width specification, using default 8"
   const byte 6963_FONT_WIDTH = 8
elsif ( !((T6963_FONT_WIDTH == 8) | (T6963_FONT_WIDTH == 6)) ) then
   _error "T6963 - Font width may only be 6 or 8" 
end if

if ( !defined(t6963_pen_color) ) then
   var bit t6963_pen_color = ON         -- default: pixel on
end if

if ( !defined(T6963_CLIPPING)) then
   const bit T6963_CLIPPING = FALSE     -- default: may show stray pixels
end if
       
-- device specific constants and variables 

const word T6963_GRAPHICS_SIZE			= T6963_X_PIXELS * T6963_Y_PIXELS
const word T6963_TEXT_SIZE				   = T6963_X_PIXELS * T6963_Y_PIXELS / 8  

const byte T6963_GRAPHICS_AREA		   = T6963_X_PIXELS / T6963_FONT_WIDTH
const byte T6963_TEXT_AREA				   = T6963_X_PIXELS / T6963_FONT_WIDTH

const byte T6963_TEXT_ROWS             = T6963_Y_PIXELS / 8

const word T6963_TEXT_HOME 			   = 0x0000       -- fixed

var   word t6963_memsize               = 0x2000       -- )
var   word t6963_graphics_home         = 0x0800       -- ) defaults!
var   byte t6963_external_cg_offset    = 0x02         -- ) 

-- --- T6963 specific commands --- ---------------

const byte T6963_SET_CURSOR_POINTER			   = 0x21
const byte T6963_SET_OFFSET_REGISTER		   = 0x22
const byte T6963_SET_ADDRESS_POINTER		   = 0x24

const byte T6963_SET_TEXT_HOME_ADDRESS		   = 0x40
const byte T6963_SET_TEXT_AREA				   = 0x41
const byte T6963_SET_GRAPHICS_HOME_ADDRESS	= 0x42
const byte T6963_SET_GRAPHICS_AREA			   = 0x43

const byte T6963_SET_MODE_OR  				   = 0x80
const byte T6963_SET_MODE_EXOR               = 0x81
const byte T6963_SET_MODE_AND                = 0x83
const byte T6963_SET_MODE_TEXT_ONLY          = 0x84
const byte T6963_SET_MODE_CG_EXTERNAL        = 0x88

const byte T6963_ATTRIBUTE_NORMAL            = 0x00
const byte T6963_ATTRIBUTE_REVERSE           = 0x05
const byte T6963_ATTRIBUTE_INHIBIT           = 0x03
const byte T6963_ATTRIBUTE_NORMAL_BLINK      = 0x08
const byte T6963_ATTRIBUTE_REVERSE_BLINK     = 0x0D
const byte T6963_ATTRIBUTE_INHIBIT_BLINK     = 0x0B

const byte T6963_DISPLAY_OFF     		      = 0x90      
const byte T6963_DISPLAY_CURSOR_BLINK_ON     = 0x91
const byte T6963_DISPLAY_CURSOR_ON  		   = 0x92
const byte T6963_DISPLAY_TEXT_ON 			   = 0x94
const byte T6963_DISPLAY_GRAPHICS_ON   	   = 0x98				

const byte T6963_CURSOR_PATTERN_SELECT		   = 0xA0

const byte T6963_DATA_AUTO_WRITE    		   = 0xB0
const byte T6963_DATA_AUTO_READ		   	   = 0xB1
const byte T6963_DATA_AUTO_RESET				   = 0xB2

const byte T6963_DATA_WRITE_AND_INCREMENT	   = 0xC0
const byte T6963_DATA_READ_AND_INCREMENT     = 0xC1
const byte T6963_DATA_WRITE_AND_DECREMENT	   = 0xC2
const byte T6963_DATA_READ_AND_DECREMENT     = 0xC3
const byte T6963_DATA_WRITE                  = 0xC4
const byte T6963_DATA_READ                   = 0xC5

const byte T6963_SCREEN_PEEK  				   = 0xE0
const byte T6963_SCREEN_COPY					   = 0xE8

const byte T6963_PIXEL_CLEAR                 = 0xF0
const byte T6963_PIXEL_SET                   = 0xF8


-- ===- PRIVATE variables, constants, procedures, functions ===
--     (supposedly not be called by user programs)


-- ---------------------------------------------------------------
-- read dataport 
-- Setting of t6963_cd by user determines data or command read
-- Upon return:
--  - the dataport is left in INPUT mode
function t6963_read_dataport() return byte is
   t6963_dataport_direction = ALL_INPUT
   t6963_wr = HIGH                         -- )
   t6963_rd = LOW                          -- ) select read mode
   t6963_ce = LOW
   _usec_delay(1)
   t6963_rd = HIGH                         -- restore quiesce state
   t6963_ce = HIGH
   return t6963_dataport                  
end function


-- ---------------------------------------------------------------
-- write dataport 
-- Setting of t6963_cd determines data or command write
-- Upon return:
--  - the dataport is left in OUTPUT mode
procedure t6963_write_dataport(byte in data) is 
   t6963_dataport = data    
   t6963_dataport_direction = ALL_OUTPUT 
   t6963_rd = HIGH                         -- )     
   t6963_wr = LOW                          -- ) select write mode   
   t6963_ce = LOW
   _usec_delay(1)
   t6963_wr = HIGH                         -- restore quiesce state
   t6963_ce = HIGH
end procedure


-- --------------------------------------------------------------
-- Spin until ready for next command or data
-- Input: Mask is the bit combination in the status byte 
--        to specify which bytes must be set 
procedure t6963_wait_until_ready_mask(byte in mask) is
   var byte data 
   t6963_cd = HIGH                         -- command (status)
   t6963_dataport_direction = ALL_INPUT
   t6963_wr = HIGH                         -- )
   t6963_rd = LOW                          -- ) select read mode
   t6963_ce = LOW
   while ((t6963_dataport & mask) != mask) loop end loop 
   t6963_rd = HIGH                            -- restore quiesce state
   t6963_ce = HIGH
end procedure


-- --------------------------------------------------------------
-- Spin until ready for next data (status bits 0 and 1 are both 1)
procedure t6963_wait_until_ready() is
   t6963_wait_until_ready_mask(1 + 2)        -- sta0 + sta1
end procedure


-- --------------------------------------------------------------
-- Spin until ready to auto-read (status bit 2 is 1)
procedure t6963_wait_until_ready_to_auto_read() is
   t6963_wait_until_ready_mask(4)            -- sta2   
end procedure


-- --------------------------------------------------------------
-- Spin until ready to auto-write (status bit 3 is 1)
procedure t6963_wait_until_ready_to_auto_write() is
   t6963_wait_until_ready_mask(8)            -- sta3
end procedure


-- --------------------------------------------------------------
procedure t6963_read_data() is
   t6963_wait_until_ready()
   t6963_cd = LOW                             -- data
   var byte data = t6963_read_dataport()      
   t6963_rd = HIGH                            -- restore quiesce state
   t6963_ce = HIGH
end procedure


-- ---------------------------------------------------------------
procedure t6963_write_data(byte in data) is
   t6963_wait_until_ready()
   t6963_cd = LOW                             -- data
   t6963_write_dataport(data)
end procedure


-- ---------------------------------------------------------------
procedure t6963_write_command(byte in cmd) is
   t6963_wait_until_ready()
   t6963_cd = HIGH                            -- command
   t6963_write_dataport(cmd)
end procedure


-- ---------------------------------------------------------------
procedure t6963_set_address(word in address) is
   var byte addr[2] at address                  -- overlay
   t6963_write_data(addr[0])                    -- LSB 
   t6963_write_data(addr[1])                    -- MSB
   t6963_write_command(T6963_SET_ADDRESS_POINTER)  
end procedure


-- ---------------------------------------------------------------
-- Clear memory region
procedure t6963_clear_memory_region(word in addr, word in amount) is
   t6963_set_address(addr)
   t6963_write_command(T6963_DATA_AUTO_WRITE)
   for (amount) loop                            
      t6963_wait_until_ready_to_auto_write()
      t6963_write_data(0)
   end loop
   t6963_write_command(T6963_DATA_AUTO_RESET) 
end procedure


-- ---------------------------------------------------------------
-- Probe RAM memory to determine the amount available RAM
-- Using steps of 2 KB
-- Writes and reads back 0x55 and 0xAA to last byte of each 2K block
-- as long as read back data matches written data.
-- Returns the number of 2KB blocks available.
function t6963_probe_memory() return word is 
   -- Function to write byte to current address and read it back. 
   -- return TRUE when matching, FALSE otherwise
   function verify(byte in x) return bit is
      var byte y
      t6963_write_data(x)
      t6963_write_command(T6963_DATA_WRITE)  -- keep address! 
      t6963_write_command(T6963_DATA_READ)
      t6963_wait_until_ready()                   
      t6963_cd = LOW                          -- data
      y = t6963_read_dataport()              -- get data
      t6963_rd = HIGH
      t6963_ce = HIGH   
      return  (y == x)                       -- TRUE when equal
   end function      
   const word step = 0x800                   -- blocks of 2 KB
   var byte blocks
   var word addr = step - 1                  -- last byte first block
   for 32 using blocks loop                  -- 32 x 2KB = 64 KB
      t6963_set_address(addr)
      if (verify(0x55) == FALSE) then
         exit loop
      elsif (verify(0xAA) == FALSE) then
         exit loop
      end if
      addr = addr + step                     -- still OK, try next block
   end loop
   return blocks
                                             -- (0 with 64 KB!)
end function
  

-- === PUBLIC procedures and functions ===========================


-- ---------------------------------------------------------------
-- Clear text memory
procedure t6963_clear_text() is
   t6963_clear_memory_region(T6963_TEXT_HOME, t6963_graphics_home - T6963_TEXT_HOME)
end procedure


-- ---------------------------------------------------------------
-- Clear graphics memory
-- Note: Works fine even with 64K memory (when t6963_memsize is 0)
procedure t6963_clear_graphics() is
   t6963_clear_memory_region(t6963_graphics_home, t6963_memsize - t6963_graphics_home - 0x800)
end procedure


-- ---------------------------------------------------------------
-- Clear screen (both text and graphics memory
procedure t6963_clear_screen() is
   t6963_clear_text()
   t6963_clear_graphics()
end procedure

-- ---------------------------------------------------------------
-- Read a byte from the current address. 
function t6963_read_next_char() return byte is
   var byte data
   t6963_write_command(T6963_DATA_READ_AND_INCREMENT)
   t6963_wait_until_ready()                   
   t6963_cd = LOW                             -- data
   data = t6963_read_dataport()              -- get it
   t6963_rd = HIGH
   t6963_ce = HIGH                      
   return data
end function


-- ---------------------------------------------------------------
-- Read a character at a specific location (column, row) in text memory
-- This procedured
function t6963_read_char(byte in column, byte in row) return byte is
   var word addr = T6963_TEXT_HOME + column + word(T6963_TEXT_AREA) * row
   t6963_set_address(addr)
   return t6963_read_next_char()
end function


-- ---------------------------------------------------------------
-- Display a character on the screen at the current location.
-- The address is incremented and subsequent t6963_write_next_char() 
-- will use the next text location and wraps automatically.
-- Notes: The ASCII values of built-in font are 32 less!
--        This difference is taken care of by this procedure and means
--        a.o. that ASCII values 0x00-0x1F (dec 0..31) cannot be displayed.
--        Example: "A" with ASCII value 0x41 is written as 0x21, 
--                 thus reading back would show 0x21.
procedure t6963_write_next_char(byte in d) is
   if ((d >= 0x20) & (d < 0x7F)) then          -- builtin table
      d = d - 0x20
   end if
   t6963_write_data(d)
   t6963_write_command(T6963_DATA_WRITE_AND_INCREMENT) 
end procedure


-- ---------------------------------------------------------------
-- Display a character at a specific location on the screen,
-- column and row are character offsets on the physical screen.
-- After the write the address is incremented. Adjacent characters
-- (e.g. of an array or string) may use t6963_write_next_char(), 
-- no need for the user to maintain the current position.
procedure t6963_write_char(byte in column, byte in row, byte in d) is
   var word addr = T6963_TEXT_HOME + column + word(T6963_TEXT_AREA) * row
   t6963_set_address(addr)
   t6963_write_next_char(d)
end procedure


-- ---------------------------------------------------------------
-- Display a string starting at a specific location on the screen,
-- column and row are character offsets on the physical screen.
-- This procedure is more efficient than writing individual adjacent characters.
procedure t6963_write_string(byte in column, byte in row, byte in string[]) is
   var word addr = T6963_TEXT_HOME + column + word(T6963_TEXT_AREA) * row
   var byte i, d
   t6963_set_address(addr)
   t6963_write_command(T6963_DATA_AUTO_WRITE)
   for count(string) using i loop
      d = string[i]
      if ((d >= 32) & (d < 128)) then          -- builtin table
         d = d - 32
      end if
      t6963_wait_until_ready_to_auto_write()
      t6963_write_data(d) 
   end loop   
   t6963_write_command(T6963_DATA_AUTO_RESET)
end procedure


-- ---------------------------------------------------------------
-- Set mode for mixed graphics and text (or text only)
-- Set one of the following constants:
--   T6963_SET_MODE_OR     		      
--   T6963_SET_MODE_EXOR     
--   T6963_SET_MODE_AND
--   T6963_SET_MODE_TEXT_ONLY  
-- Optionally combined (or-ed) with characterset selection:
--   T6963_SET_MODE_CG_EXTERNAL
procedure t6963_set_mode(byte in mode) is
   t6963_write_command(T6963_SET_MODE_OR | (mode & 0x0F))      -- ensure correct command!
end procedure


-- ---------------------------------------------------------------
-- Set display mode
-- Set any or a combination (or-ed) of the following constants:
--   T6963_DISPLAY_OFF     		      
--   T6963_DISPLAY_CURSOR_BLINK_ON     
--   T6963_DISPLAY_CURSOR_ON  		   
--   T6963_DISPLAY_TEXT_ON 			   
--   T6963_DISPLAY_GRAPHICS_ON   
-- Note: To switch one of these off de-select it!
procedure t6963_display_mode(byte in mode) is
   t6963_write_command(T6963_DISPLAY_OFF | (mode & 0x0F))      -- ensure correct command!
end procedure


-- ---------------------------------------------------------------
-- Set cursor pattern
-- Input: height of the cursor: number of pixels (1..8)
-- Notes: - Cursor ON/OFF/BLINK must be selected with t6963_display_mode)
--        - lines=1 is bottom line, lines=2 bottom 2 lines, lines=8 block cursor
procedure t6963_set_cursor_pattern(byte in lines) is
   t6963_write_command(T6963_CURSOR_PATTERN_SELECT | ((lines - 1) & 0x07))
end procedure


-- ---------------------------------------------------------------
-- Set cursor position
-- Input: column and row of the cursor
-- Notes: - Cursor ON/OFF/BLINK must be selected with t6963_display_mode.
--        - The cursor does not automatically move when writing characters!
procedure t6963_set_cursor_position(byte in column, byte in row) is
   var word addr
   var byte aaddr[2] at addr
   aaddr[0] = column
   aaddr[1] = row
   t6963_set_address(addr)
   t6963_write_command(T6963_SET_CURSOR_POINTER)
end procedure


-- ---------------------------------------------------------------
-- Set attribute
-- Input: (Start-)column and row of the attribyte, 
--        the atribute byte and the number of atttributes.
--        The attribyte byte may be one of the following:
--        T6963_ATTRIBUTE_NORMAL            
--        T6963_ATTRIBUTE_REVERSE
--        T6963_ATTRIBUTE_INHIBIT
--        T6963_ATTRIBUTE_NORMAL_BLINK
--        T6963_ATTRIBUTE_REVERSE_BLINK
--        T6963_ATTRIBUTE_INHIBIT_BLINK
-- Notes: - Attribute bytes are bytes in graphics memory.
--        - The controller is set to text mode only (required with attributes).
--        - Text and graphics display must be set on by the user program!
--          with or without cursor(blinking).
--        - Graphics memory may have to be erased when starting with setting attributes.
procedure t6963_set_attribute(byte in column, byte in row, byte in attr, byte in num) is
   var word addr = T6963_GRAPHICS_HOME + column + word(T6963_GRAPHICS_AREA) * row
   t6963_write_command(T6963_SET_MODE_TEXT_ONLY)   -- ensure attribute mode
   t6963_set_address(addr)
   t6963_write_command(T6963_DATA_AUTO_WRITE)
   for num loop                            
      t6963_wait_until_ready_to_auto_write()
      t6963_write_data(attr) 
   end loop   
   t6963_write_command(T6963_DATA_AUTO_RESET)
end procedure


-- ---------------------------------------------------------------
-- Read font pattern(s) from CG RAM
-- The pattern of one CG RAM character consists of 8 bytes.
-- This procedure allows to read one or more font patterns, 
-- depending on the size of the specified array.
-- The whole rangeof ASCII values can be read, both 
-- internal (0x00-7F) and external (0c80..FF) CG-RAM.
procedure t6963_read_cg_memory(byte in x, byte in pattern[]) is
   var byte i
   var word addr = (word(t6963_external_cg_offset) << 11) + (word(x) * 8)
   t6963_set_address(addr)
   t6963_write_command(T6963_DATA_AUTO_READ)
   for count(pattern) using i loop
      t6963_wait_until_ready_to_auto_read()
      t6963_cd = LOW                             -- data
      pattern[i] = t6963_read_dataport()        -- store byte in array
      t6963_rd = HIGH                            -- restore quiesce state
      t6963_ce = HIGH
   end loop   
   t6963_write_command(T6963_DATA_AUTO_RESET)
end procedure


-- ---------------------------------------------------------------
-- Write font pattern(s) to CG-RAM
-- This procedure allows to write font patterns for one or more 
-- characters in one operation. It takes the specified character ASCII 
-- value as the first position and writes as many patterns as are in
-- the pattern array (most likely 8 or a multiple of 8 bytes).   
-- The characters after the first will have subsequent character values.  
-- Input: - Character (ASCII value)  
--        - Array with bitpatterns (8 bytes per CG character, each of
--          the bytes represents a horizontal line of 8 pixels.
-- Notes: - This library uses the internal font, which means:
--           - characters 0x00..0x7F are fixed (builtin the T6963 chip)
--           - characters 0x80..0xFF are user defined.
procedure t6963_write_cg_memory(byte in ascii, byte in pattern[]) is
   if (ascii < 0x80) then                       -- must be in range 0x80..FF
      return
   end if
   var word addr = (word(t6963_external_cg_offset) << 11) + (word(ascii) * 8)
   var byte i
   t6963_set_address(addr)
   t6963_write_command(T6963_DATA_AUTO_WRITE)
   for count(pattern) using i loop
      t6963_wait_until_ready_to_auto_write()
      t6963_write_data(pattern[i])
   end loop   
   t6963_write_command(T6963_DATA_AUTO_RESET)
end procedure


-- ---------------------------------------------------------------
-- Display or clear a single pixel in graphics 
-- (x,y) are pixel offset on the physical screen
procedure t6963_write_pixel(byte in x, byte in y) is
   if (T6963_CLIPPING) then
      if ( (x >= T6963_X_PIXELS) | (y >= T6963_Y_PIXELS) ) then
         return
      end if
   end if
   var byte byte_addr  = x / T6963_FONT_WIDTH
   var byte bit_offset = x % T6963_FONT_WIDTH
   bit_offset = T6963_FONT_WIDTH - 1 - bit_offset  -- left-to-right!
   var word addr = t6963_graphics_home + byte_addr + word(T6963_GRAPHICS_AREA) * y
   t6963_set_address(addr)
   if (t6963_pen_color == ON) then 
      t6963_write_command(T6963_PIXEL_SET | bit_offset)
   else
      t6963_write_command(T6963_PIXEL_CLEAR | bit_offset)
   end if
end procedure


-- ---------------------------------------------------------------
-- Initialize t6963
-- Control signals use negative logic  (high is inactive)
-- All control pins must be set for output by the application 
procedure t6963_init() is
   t6963_reset = LOW                         
   delay_1ms(10)                             -- safe reset period
   t6963_reset = HIGH
   
   t6963_wr = HIGH                            -- )
   t6963_rd = HIGH                            -- ) 
   t6963_cd = HIGH                            -- ) quiesced state
   t6963_ce = HIGH                            -- )  
   if (defined(t6963_fs)) then                -- font selection pin alias is optional         
      if (T6963_FONT_WIDTH == 6) then
         t6963_fs = HIGH                   -- 6x8 font
      else
         t6963_fs = LOW                    -- 8x8 font  
      end if
   end if
   t6963_set_address(T6963_TEXT_HOME)
   t6963_write_command(T6963_SET_TEXT_HOME_ADDRESS)
   t6963_set_address(t6963_graphics_home)           
   t6963_write_command(T6963_SET_GRAPHICS_HOME_ADDRESS)             
   t6963_set_address(word(T6963_TEXT_AREA))
   t6963_write_command(T6963_SET_TEXT_AREA)
   t6963_set_address(word(T6963_GRAPHICS_AREA))   
   t6963_write_command(T6963_SET_GRAPHICS_AREA)   
   t6963_write_command(T6963_SET_MODE_OR)           -- using internal font (default)
   t6963_set_address(word(t6963_external_cg_offset))   
   t6963_write_command(T6963_SET_OFFSET_REGISTER)
   t6963_write_command(T6963_DISPLAY_TEXT_ON | T6963_DISPLAY_GRAPHICS_ON)      
   
   -- Now determine total amount of RAM by probing and 
   -- modify initial settings of graphics home and offset register    
   -- Note: memory probing seems to works only correctly after a complete init 
   t6963_memsize = 0x800 * t6963_probe_memory() 
   if (t6963_memsize > 0) then                        -- less than 64 KB
      t6963_graphics_home = t6963_memsize / 4         -- 1st quarter for text 
   else                                               -- 64 KB    
      t6963_graphics_home = 0x4000                    -- 16 of 64 KB for text   
   end if 
   t6963_set_address(t6963_graphics_home)
   t6963_write_command(T6963_SET_GRAPHICS_HOME_ADDRESS)                     
   t6963_external_cg_offset = byte((t6963_memsize - 0x800) >> 11)   -- CG RAM in upper 2 KB memory       
   t6963_set_address(word(t6963_external_cg_offset))           
   t6963_write_command(T6963_SET_OFFSET_REGISTER)        
         
   t6963_clear_screen()
      
end procedure



